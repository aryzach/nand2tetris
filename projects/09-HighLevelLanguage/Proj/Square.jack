class Square {
	field int size;
	field int x,y;

	constructor Square new(int s, int Ax, int Ay) {
		let size = s;
		let x = Ax;
		let y = Ay;
		do draw();
		return this;
	}

	method int getX() { return x; }
	method int getY() { return y; }
	method int getSize() { return size; }

	method void dispose() {
		do Memory.deAlloc(this);
		return;
	}

	method void draw() {
		// use OS to draw size at loc
		do Screen.setColor(true);
		do Screen.drawRectangle(x, y, x + size, y + size);
		return;
	}

	method void erase() {
		do Screen.setColor(false);
                do Screen.drawRectangle(x, y, x + size, y + size);
                return;
	}

	method void increaseSize() {
		if (((y + size) < 254) & ((x + size) < 510)) {
			do erase();
			let size = size + 1;
			do draw();
		}
		return;
	}

	method void decreaseSize() {
                if (size > 10) { 
                        do erase();
                        let size = size - 1;
                        do draw();
                }
                return;
        }


	method void moveUp() {
		if (y > 1) {
			do Screen.setColor(false);
         		do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
         		let y = y - 1;
         		do Screen.setColor(true);
         		do Screen.drawRectangle(x, y, x + size, y + 1);
      		}
        	return;
	}

	method void moveDown() {
      if ((y + size) < 254) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, y, x + size, y + 1);
         let y = y + 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, (y + size) - 1, x + size, y + size);
      }
      return;
   }

	 method void moveLeft() {
      if (x > 1) {
         do Screen.setColor(false);
         do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
         let x = x - 1;
         do Screen.setColor(true);
         do Screen.drawRectangle(x, y, x + 1, y + size);
      }
      return;
   }

   /** Moves the square right by 2 pixels. */
   method void moveRight() {
      if ((x + size) < 510) {
         do Screen.setColor(false);
         do Screen.drawRectangle(x, y, x + 1, y + size);
         let x = x + 1;
         do Screen.setColor(true);
         do Screen.drawRectangle((x + size) - 1, y, x + size, y + size);
      }
      return;
   }
}


















